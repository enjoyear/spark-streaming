group 'com.chen.guo'
version '1.0-SNAPSHOT'

apply from: './gradle/scripts/jvm-args-java17.gradle'
def jvmArgsJava17 = ext.jvmArgsJava17

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        // Pick version compatible with your Gradle (see note below)
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2" // for Gradle 7.x
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"    // 0.9.x works on Gradle 7.x
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'idea'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.google.protobuf'

    repositories {
        mavenCentral()
    }

    ext.versions = [scalaMajor     : '2.13',
                    scala          : '2.13.17',
                    spark          : '4.0.0',
                    protobufVersion: '3.25.8',
                    grpcVersion    : '1.76.0']

    dependencies {
        implementation "org.apache.spark:spark-core_${versions.scalaMajor}:${versions.spark}"
        implementation "org.apache.spark:spark-sql_${versions.scalaMajor}:${versions.spark}"
        implementation "org.apache.spark:spark-streaming_${versions.scalaMajor}:${versions.spark}"

        // Databricks protobuf API is not the same as the oss version
        // implementation "org.apache.spark:spark-protobuf_${versions.scalaMajor}:${versions.spark}"

        implementation "org.apache.spark:spark-sql-kafka-0-10_${versions.scalaMajor}:${versions.spark}"
        implementation "org.apache.spark:spark-streaming-kafka-0-10_${versions.scalaMajor}:${versions.spark}"
        //implementation "org.apache.kafka:kafka-clients:2.7.0"

        implementation "io.delta:delta-core_${versions.scalaMajor}:1.1.0"

        implementation "org.scala-lang:scala-library:${versions.scala}"
        implementation "org.scala-lang:scala-reflect:${versions.scala}"

        implementation "com.google.protobuf:protobuf-java:${versions.protobufVersion}"
        implementation "io.grpc:grpc-stub:${versions.grpcVersion}"
        implementation "io.grpc:grpc-protobuf:${versions.grpcVersion}"

        // slf4j can print "org.apache.kafka" and "org.apache.spark" log for some reason
//        implementation "org.slf4j:slf4j-api:1.7.30"
//        implementation "org.slf4j:slf4j-simple:1.7.30"
//        implementation "org.apache.logging.log4j:log4j-api:2.11.2"
//        implementation "org.apache.logging.log4j:log4j-core:2.11.2"

        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

        testImplementation "org.scalatest:scalatest_${versions.scalaMajor}:3.2.5"
        testImplementation group: 'org.testng', name: 'testng', version: '7.4.0'
    }

    test {
        useTestNG()

        afterTest { desc, result ->
            logger.quiet "==> Test ${result.resultType}: ${desc.name} at ${desc.className}"
        }
    }
    compileJava {
        options.release = 17
        options.forkOptions.jvmArgs += jvmArgsJava17
    }

    protobuf {
        // protoc executable
        protoc {
            // download from repo
            artifact = "com.google.protobuf:protoc:${versions.protobufVersion}"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpcVersion}"
            }
        }
        generateProtoTasks {
            // all() returns the collection of all protoc tasks
            all().each { task ->
                // If true, will generate a descriptor_set.desc file under
                // task.outputBaseDir. Default is false.
                // See --descriptor_set_out in protoc documentation about what it is.
                task.generateDescriptorSet = true

                // Allows to override the default for the descriptor set location
                task.descriptorSetOptions.path =
                        "${projectDir}/build/descriptors/${task.sourceSet.name}.desc"

                // If true, the descriptor set will contain line number information
                // and comments. Default is false.
                task.descriptorSetOptions.includeSourceInfo = false

                // If true, the descriptor set will contain all transitive imports and
                // is therefore self-contained. Default is false.
                task.descriptorSetOptions.includeImports = true
                task.builtins {
                    java {}
                }
                task.plugins {
                    grpc {}
                }
            }
        }
    }

    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir "${projectDir}/src/main/protobuf"
            }
            java {
                srcDirs "${protobuf.generatedFilesBaseDir}/main/grpc"
                srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
            }
        }
        test {
            proto {
                srcDir "${projectDir}/src/test/resources/protobuf"
            }
            java {
                srcDirs "${protobuf.generatedFilesBaseDir}/test/java"
            }
        }
    }

    shadowJar {
        mergeServiceFiles()
        relocate('com.google.common', 'chen.guo.shadow.com.google.common')
    }
}